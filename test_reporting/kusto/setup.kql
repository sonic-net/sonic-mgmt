# Kusto script to setup a Kusto database for storing test results
#
# See https://docs.microsoft.com/en-us/azure/data-explorer/ingest-json-formats
# for details about how Kusto maps JSON test results to tabular formats.

###############################################################################
# TOP-LEVEL DATABASE CONFIGURATION                                            #
# 1. Retain data for 1 year                                                   #
# 2. Authorize an AAD client to push data to Kusto                            #
###############################################################################
.alter-merge database SonicTestData policy retention softdelete = 365d

# See: https://docs.microsoft.com/en-us/azure/data-explorer/provision-azure-ad-app
.add database <DB Name> ingestors ('aadapp=<AAD Client ID>;<domain>') '<Service Name>'

###############################################################################
# TESTCASE TABLE SETUP                                                        #
# 1. Create a RawTestCases table to feed data into TestCases                  #
# 2. Create a function to expand the data from RawTestCases                   #
# 3. Create a TestCases table                                                 #
# 4. Setup a policy to automatically ingest data from RawTestCases            #
###############################################################################
.create table RawTestCases (Cases: dynamic)

# See: https://docs.microsoft.com/en-us/azure/data-explorer/ingest-json-formats#ingest-json-records-containing-arrays
.create table RawTestCases ingestion json mapping 'RawCaseMappingV1' '[{"column":"Cases","Properties":{"path":"$"}}]'

# Retain records for 1 day to aid with debugging
.alter-merge table RawTestCases policy retention softdelete = 1d recoverability = disabled

.create function ExpandTestCases() {
    RawTestCases
    | mv-expand cases = Cases.cases
    | project
        Feature = tostring(cases["feature"]),
        TestCase = tostring(cases["name"]),
        ModulePath = tostring(cases["classname"]),
        FilePath = tostring(cases["file"]),
        StartLine = toint(cases["line"]),
        Runtime = todouble(cases["time"]),
        Result = tostring(cases["result"]),
        ReportId = tostring(cases["id"]),
	    Error = tobool(cases["error"])
}

.create table TestCases (Feature: string, TestCase: string, ModulePath: string,
                         FilePath: string, StartLine: int, Runtime: double, Result: string,
                         ReportId: string, Error: bool)

.alter table TestCases policy update @'[{"Source": "RawTestCases", "Query": "ExpandTestCases()", "IsEnabled": "True"}]'

###############################################################################
# SUMMARY TABLE SETUP                                                         #
# 1. Create a TestReportSummary table to store test summaries                 #
# 2. Add a JSON mapping for the table                                         #
###############################################################################
.create table TestReportSummary (ReportId: string, TotalCasesRun: int, Failures: int,
                                 Errors: int, Skipped: int, TotalRuntime: double)

.create table TestReportSummary ingestion json mapping 'FlatSummaryMappingV1' '[{"column":"ReportId","Properties":{"path":"$.id"}},
                                                                                {"column":"TotalCasesRun","Properties":{"path":"$.tests"}},
                                                                                {"column":"Failures","Properties":{"path":"$.failures"}},
                                                                                {"column":"Errors","Properties":{"path":"$.errors"}},
                                                                                {"column":"Skipped","Properties":{"path":"$.skipped"}},
                                                                                {"column":"TotalRuntime","Properties":{"path":"$.time"}}]'

###############################################################################
# METADATA TABLE SETUP                                                        #
# 1. Create a TestReportMetadata table to store test metadata                 #
# 2. Add a JSON mapping for the table                                         #
###############################################################################
.create table TestReportMetadata (Timestamp: datetime, AsicType: string,
                                  HardwareSku: string, OSVersion: string,
                                  Platform: string, Topology: string,
                                  TrackingId: string, ReportId: string,
                                  TestbedName: string, UploadTimestamp: datetime)

.create table TestReportMetadata ingestion json mapping 'FlatMetadataMappingV1' '[{"column":"Timestamp","Properties":{"path":"$.timestamp"}},
                                                                                  {"column":"AsicType","Properties":{"path":"$.asic"}},
                                                                                  {"column":"HardwareSku","Properties":{"path":"$.hwsku"}},
                                                                                  {"column":"OSVersion","Properties":{"path":"$.os_version"}},
                                                                                  {"column":"Platform","Properties":{"path":"$.platform"}},
                                                                                  {"column":"Topology","Properties":{"path":"$.topology"}},
                                                                                  {"column":"TrackingId","Properties":{"path":"$.tracking_id"}},
                                                                                  {"column":"ReportId","Properties":{"path":"$.id"}},
                                                                                  {"column":"TestbedName","Properties":{"path":"$.testbed"}},
                                                                                  {"column":"UploadTimestamp","Properties":{"path":"$.upload_time"}}]'


###############################################################################
# RAW REBOOT REPORT TABLE SETUP                                               #
# 1. Create a RawRebootTimingData table to store reboot test timings          #
# 2. Add a JSON mapping for the table                                         #
###############################################################################
// Create table command
////////////////////////////////////////////////////////////
.create table ['RawRebootTimingData']  (['ProcessingTime']:dynamic, ['FirstOccurence']:dynamic, ['RebootTime']:dynamic, ['Dataplane']:dynamic)

// Create mapping command
////////////////////////////////////////////////////////////
.create table ['RawRebootTimingData'] ingestion json mapping 'RawRebootTimingDataMapping' '[
                                                                                {"column":"ProcessingTime", "Properties":{"Path":"$[\'processing_time\']"}},
                                                                                {"column":"FirstOccurence", "Properties":{"Path":"$[\'first_occurence\']"}},
                                                                                {"column":"RebootTime", "Properties":{"Path":"$[\'reboot_time\']"}},
                                                                                {"column":"Dataplane", "Properties":{"Path":"$[\'dataplane\']"}}]'

###############################################################################
# REBOOT REPORT TABLE SETUP                                                   #
# 1. Create a RebootTimingData table to store reboot test timings             #
# 2. Add a JSON mapping for the table                                         #
###############################################################################
// Create table command
////////////////////////////////////////////////////////////
.create table ['RebootTimingData']  (['RebootTime']:string, ['RebootType']:string, ['DataplaneLongestDowntime']:string,
                                    ['DataplaneLostPackets']:string, ['DataplaneTotalDowntime']:string, ['ProcessingTimeDefaultRouteSet']:string,
                                    ['ProcessingTimeSaiSwitchCreate']:string, ['ProcessingTimeLagReady']:string, ['ProcessingTimeBgp']:string,
                                    ['ProcessingTimeSyncd']:string, ['ProcessingTimeRadv']:string, ['ProcessingTimeFirstNeighborEntry']:string,
                                    ['ProcessingTimeInitView']:string, ['ProcessingTimeFinalizer']:string, ['ProcessingTimePortInit']:string,
                                    ['ProcessingTimeTeamd']:string, ['ProcessingTimeGbsyncd']:string, ['ProcessingTimeSwss']:string,
                                    ['ProcessingTimeApplyView']:string)


// Create mapping command
////////////////////////////////////////////////////////////
.create table ['RebootTimingData'] ingestion json mapping 'RebootTimingDataMapping' '[{"column":"RebootTime", "Properties":{"Path":"$[\'reboot_time\']"}},
                                                                                    {"column":"RebootType", "Properties":{"Path":"$[\'reboot_type\']"}},
                                                                                    {"column":"DataplaneLongestDowntime", "Properties":{"Path":"$[\'dataplane\'][\'longest_downtime\']"}},
                                                                                    {"column":"DataplaneLostPackets", "Properties":{"Path":"$[\'dataplane\'][\'lost_packets\']"}},
                                                                                    {"column":"DataplaneTotalDowntime", "Properties":{"Path":"$[\'dataplane\'][\'total_downtime\']"}},
                                                                                    {"column":"ProcessingTimeDefaultRouteSet", "Properties":{"Path":"$[\'processing_time\'][\'default_route_set\']"}},
                                                                                    {"column":"ProcessingTimeSaiSwitchCreate", "Properties":{"Path":"$[\'processing_time\'][\'sai_switch_create\']"}},
                                                                                    {"column":"ProcessingTimeLagReady", "Properties":{"Path":"$[\'processing_time\'][\'lag_ready\']"}},
                                                                                    {"column":"ProcessingTimeBgp", "Properties":{"Path":"$[\'processing_time\'][\'bgp\']"}},
                                                                                    {"column":"ProcessingTimeSyncd", "Properties":{"Path":"$[\'processing_time\'][\'syncd\']"}},
                                                                                    {"column":"ProcessingTimeRadv", "Properties":{"Path":"$[\'processing_time\'][\'radv\']"}},
                                                                                    {"column":"ProcessingTimeFirstNeighborEntry", "Properties":{"Path":"$[\'processing_time\'][\'first_neighbor_entry\']"}},
                                                                                    {"column":"ProcessingTimeInitView", "Properties":{"Path":"$[\'processing_time\'][\'init_view\']"}},
                                                                                    {"column":"ProcessingTimeFinalizer", "Properties":{"Path":"$[\'processing_time\'][\'finalizer\']"}},
                                                                                    {"column":"ProcessingTimePortInit", "Properties":{"Path":"$[\'processing_time\'][\'port_init\']"}},
                                                                                    {"column":"ProcessingTimeTeamd", "Properties":{"Path":"$[\'processing_time\'][\'teamd\']"}},
                                                                                    {"column":"ProcessingTimeGbsyncd", "Properties":{"Path":"$[\'processing_time\'][\'gbsyncd\']"}},
                                                                                    {"column":"ProcessingTimeSwss", "Properties":{"Path":"$[\'processing_time\'][\'swss\']"}},
                                                                                    {"column":"ProcessingTimeApplyView", "Properties":{"Path":"$[\'processing_time\'][\'apply_view\']"}}]'
