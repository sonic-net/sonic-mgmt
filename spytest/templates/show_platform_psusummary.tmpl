Value psu (\w+\s\d+)
Value psu_status (\S+\s*\S+|\S+)
Value manufacturer_id (\S+\s*\S+|\S+)
Value model (\S+)
Value serial_number (\S+)
Value output_voltage (\S+)
Value output_current (\S+)
Value output_power (\S+)
Value fan_direction (\S+)
Value fan_speed (\d+)

Start
  ^\s*${psu}:\s+${psu_status}\s*$$ -> CLICK
  ^\s*${psu}:\s*$$ -> KLISH

CLICK
  ^\s*Manufacturer\s*Id:\s*${manufacturer_id}\s*$$
  ^\s*Model:\s*${model}\s*$$
  ^\s*Serial\s*Number:\s*${serial_number}\s*$$
  ^\s*Output\s*Voltage\s*\(mV\):\s*${output_voltage}\s*$$
  ^\s*Output\s*Current\s*\(mA\):\s*${output_current}\s*$$
  ^\s*Output\s*Power\s*\(mW\):\s*${output_power}\s*$$
  ^\s*Fan\s*Direction:\s*${fan_direction}\s*$$
  ^\s*Fan\s*Speed\s*\(RPM\):\s*${fan_speed}\s*$$
  ^$$ -> Record Start

KLISH
  ^\s*Description\s*:${model}\s*$$
  ^\s*Mfg Name\s*:${manufacturer_id}\s*$$
  ^\s*Oper Status\s*:${psu_status}\s*$$
  ^\s*Serial Number\s*:${serial_number}\s*$$
  ^\s*Output Current\s*\(A\)\s*:${output_current}\s*$$
  ^\s*Output Power\s*\(W\)\s*:${output_power}\s*$$
  ^\s*Output Voltage\s*\(V\)\s*:${output_voltage}\s*$$
  ^\s*Fan Speed\s*\(RPM\)\s*:0\s*$$ -> Record Start
  ^\s*Fan Speed\s*\(RPM\)\s*:${fan_speed}\s*$$
  ^\s*Fan Direction\s*:${fan_direction}\s*$$ -> Record Start