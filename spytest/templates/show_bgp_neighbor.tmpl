Value Required NEIGHBORIP ([\:\d\w\.]+)
Value REMOTEASN (\d+)
Value LOCALASN (\d+)
Value BGPVERSION (\d+)
Value REMROUTERID (\S+)
Value LOCALROUTERID (\S+)
Value STATE (\S+)
Value LASTREAD (\S+)
Value LASTWRITE (\S+)
Value HOLDTIME (\d+)
Value KEEPALIVE (\d+)
Value INQDEPTH (\d+)
Value OUTQDEPTH (\d+)
Value OPENSENT (\S+)
Value OPENRCVD (\S+)
Value NOTIFICATIONSENT (\S+)
Value NOTIFICATIONRCVD (\S+)
Value UPDATESENT (\S+)
Value UPDATERCVD (\S+)
Value KEEPALIVESENT (\S+)
Value KEEPALIVERCVD (\S+)
Value ROUTEREFRESHSENT (\S+)
Value ROUTEREFRESHRCVD (\S+)
Value CAPABLITYSENT (\S+)
Value CAPABILITYRCVD (\S+)
Value SENTTOTAL (\S+)
Value RCVDTOTAL (\S+)
Value PEERGROUP (\S+)
Value ASBYTE (\S+)
Value IPV4UCASTNCAP ([\S\s]+)
Value L2VPNEVPNNCAP ([\S\s]+)
Value ENDOFRIBSEND ([\S\s]+)
Value ENDOFRIBRCV ([\S\s]+)
Value GRCAPABILITY ([\S\s]+)
Value BGPDOWNREASON ([\S\s]+)
Value BGPLASTRESET ([\d\:]+)
Value BFDTYPE (\S+)
Value BFDMultiplier (\S+)
Value BFDRXINTR (\S+)
Value BFDTXINTR (\S+)
Value BFDSTATUS (\S+)
Value BFDLASTUPDATE (\S+)
Value List UPDTGRP (\d+)
Value List SUBGRP (\d+)
Value List PKTQL (\d+)
Value List ACCEPTPREFIX (\d+)


Start
  ^\s*BGP\s*neighbor\s*is\s*(.*) -> Continue.Record
  ^\s*BGP\s*neighbor\s*is\s*${NEIGHBORIP},\s*remote\s*AS\s*${REMOTEASN},\s*local\s*AS\s*${LOCALASN},\s*external\s*link
  ^\s*BGP\s*neighbor\s*is\s*${NEIGHBORIP},\s*remote\s*AS\s*${REMOTEASN},\s*local\s*AS\s*${LOCALASN},\s*internal\s*link
  ^\s*Member of peer-group ${PEERGROUP} for session parameters\s*$$
  ^\s*BGP\s+version\s+${BGPVERSION},\s+remote\s+router\s+ID\s+${REMROUTERID}\s?,\s+local\s+router\s+ID\s+${LOCALROUTERID}\s*$$
  ^\s*BGP\s+state\s+=\s+${STATE},\s*up\s*for\s*.*
  ^\s*BGP\s+state\s+=\s+${STATE}
  ^\s*Last\s+read\s+${LASTREAD},\s+Last\s+write\s+${LASTWRITE}
  ^\s*Hold time is ${HOLDTIME}, keepalive interval is ${KEEPALIVE} seconds\s*$$
  ^\s*Hold time is ${HOLDTIME}\s+seconds, keepalive interval is ${KEEPALIVE} seconds\s*$$
  ^\s*Neighbor capabilities:
  ^\s*${ASBYTE} Byte AS: advertised
  ^\s*Address Family IPv4 Unicast: ${IPV4UCASTNCAP}
  ^\s*Address Family L2VPN EVPN: ${L2VPNEVPNNCAP}
  ^\s*Graceful restart information:
  ^\s*End-of-RIB send: ${ENDOFRIBSEND}
  ^\s*End-of-RIB received: ${ENDOFRIBRCV}
  ^\s*Graceful Restart Capabilty: ${GRCAPABILITY}
  ^\s*Message statistics:\s*$$
  ^\s*In[qQ] depth is ${INQDEPTH}\s*$$
  ^\s*Out[qQ] depth is ${OUTQDEPTH}\s*$$
  ^\s*Sent\s*Rcvd\s*$$
  ^\s*Opens:\s*${OPENSENT}\s*${OPENRCVD}\s*$$
  ^\s*Notifications:\s*${NOTIFICATIONSENT}\s*${NOTIFICATIONRCVD}\s*$$
  ^\s*Updates:\s*${UPDATESENT}\s*${UPDATERCVD}\s*$$
  ^\s*Keepalive(s?):\s*${KEEPALIVESENT}\s*${KEEPALIVERCVD}\s*$$
  ^\s*Route Refresh:\s*${ROUTEREFRESHSENT}\s*${ROUTEREFRESHRCVD}\s*$$
  ^\s*Capability:\s*${CAPABLITYSENT}\s*${CAPABILITYRCVD}\s*$$
  ^\s*Total:\s*${SENTTOTAL}\s*${RCVDTOTAL}\s*$$
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*Notification\s*sent\s*\(\s*${BGPDOWNREASON}\)
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*due\s*to\s*NOTIFICATION\s*sent\s*\(\s*${BGPDOWNREASON}\)
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*due\s*to\s*NOTIFICATION\s*received\s*\(\s*${BGPDOWNREASON}\)
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*due\s*to\s*${BGPDOWNREASON}
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*Last\s*reset\s*reason\s*${BGPDOWNREASON}$$
  ^\s*Last\s*reset\s*${BGPLASTRESET},\s*${BGPDOWNREASON}$$
  ^\s*BFD: Type: ${BFDTYPE} hop
  ^\s*Detect Multiplier: ${BFDMultiplier}, Min Rx interval: ${BFDRXINTR}, Min Tx interval: ${BFDTXINTR}
  ^\s*Status: ${BFDSTATUS}, Last update: ${BFDLASTUPDATE}\s*$$
  ^\s*Update group ${UPDTGRP}, subgroup ${SUBGRP}
  ^\s*Packet Queue length ${PKTQL}
  ^\s*${ACCEPTPREFIX} accepted prefixes$$




